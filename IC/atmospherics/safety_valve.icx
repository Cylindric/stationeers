# SAFETY VALVE
# ============
#
# This code can be used to protect a pipe network
# from exploding due to over-pressure conditions.
# 
# The pressure of the system is read from an attached pressure-reading
# device such as a Pipe Analyser or a Tank.
#
# On an over-pressure event, the dump valve is "enabled" venting the
# system down to safe levels, and the feed valve is "disabled" 
# preventing further pressure increases.
#
# On an under-pressure event, the dump valve is "disabled", and the
# feed valve is enabled, allowing the system to rise in pressure.
#
# Attach the following devices to the IC Socket. Devices marked with a * are optional:
#   D0  - Pressure sensor (either a Pipe Analyser or a Tank)
#   D1* - Dump valve (a Pressure Regulator or Digital Valve)
#   D2* - Feed valve (the valve or mixer that is causing the over-pressure)
#   D3* - Over-Pressure Indicator (something like a Light)
#   D4* - Under-Pressure Indicator (something like a Light)
#   D5* - Chiller valve (either active cooling device or a valve)

use loop
use aliases

alias PressureSensor d0
alias DumpValve d1
alias FeedValve d2
alias OverPressureIndicator d3
alias UnderPressureIndicator d4
alias ChillerValve d5

const MaxPressure = 50000.0 # Small tank max is 60,000kPa
const MinPressure = 8500.0
const ChillMinTemperature = 19 + 274.15
const ChillMaxTemperature = 21 + 274.15

# Read current pressure value
var CurrentPressure = PressureSensor.Pressure
var CurrentTemperature = PressureSensor.Temperature

# If pressure is high, open dump valve
if CurrentPressure > MaxPressure
    bdseal DumpValve EnableDumpValve
    bdseal FeedValve DisableFeedValve
    bdseal OverPressureIndicator EnableOverPressureIndicator
    bdseal UnderPressureIndicator DisableOverPressureIndicator
end

# If pressure is low, close dump valve
if CurrentPressure < MinPressure
    bdseal DumpValve DisableDumpValve
    bdseal FeedValve EnableFeedValve
    bdseal OverPressureIndicator DisableOverPressureIndicator
    bdseal UnderPressureIndicator EnableOverPressureIndicator
end

# If temperature is high, open the chiller valve
if CurrentTemperature > ChillMaxTemperature
    bdseal ChillerValve EnableChillerValve
end

# If temperature is low, close the chiller valve
if CurrentTemperature < ChillMinTemperature
    bdseal ChillerValve DisableChillerValve
end

#################################################################

function EnableDumpValve
    DumpValve.On = 1
end

function DisableDumpValve
    DumpValve.On = 0
end

function EnableChillerValve
    ChillerValve.On = 1
end

function DisableChillerValve
    ChillerValve.On = 0
end

function EnableFeedValve
    FeedValve.On = 1
end

function DisableFeedValve
    FeedValve.On = 0
end

function EnableOverPressureIndicator
    OverPressureIndicator.On = 1
end

function DisableOverPressureIndicator
    OverPressureIndicator.On = 0
end

function EnableUnderPressureIndicator
    UnderPressureIndicator.On = 1
end

function DisableUnderPressureIndicator
    UnderPressureIndicator.On = 0
end