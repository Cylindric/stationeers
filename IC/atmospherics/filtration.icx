# FILTRATION MODULE
# To be installed into Atmospherics Filtration Units
#
# If the ratio of the filtered gas in the input is too low, then
# filtration will pause (after a short delay).
#
# If the pressure on the output pipe is too high, filtration will
# stop immediately.
#
# The gas ratio measured and show depends on the cartridge type
# currently in the left slot of the Filtration device.
#
# Attach the following devices to the IC Socket (*optional):
#   D0 - A console that will display current Filter Cartidge level
#   D1 - A console that will display current input gas ratio

use aliases


alias AverageFilterDisplay d0
alias InputRatioDisplay d1

# SETTINGS
const MIN_INPUT_MOLES = 0.0
const MAX_OUTPUT_PRESSURE = 10000

# STATES
const STATE_FILTERING = 0
const STATE_OVERPRESSURE = 1
const STATE_IDLE = 2
const STATE_WAITING = 3

# COLOURS
const COLOR_BLUE = 0
const COLOR_GREY = 1
const COLOR_GREEN = 2
const COLOR_ORANGE = 3
const COLOR_RED = 4
const COLOR_YELLOW = 5
const COLOR_WHITE = 6
const COLOR_BLACK = 7
const COLOR_BROWN = 8
const COLOR_KHAKI = 9
const COLOR_PINK = 10
const COLOR_PURPLE = 11

# CONSOLE MODE
const MODE_DEFAULT = 0
const MODE_PERCENTAGE = 1
const MODE_POWER = 2

# FILTRATION SLOTS
const FILTER_SLOT_RIGHT = 0
const FILTER_SLOT_LEFT = 1

# FILTER CANISTERS
const FILTER_CARBON_DIOXIDE1 = 1635000764
const FILTER_CARBON_DIOXIDE2 = 416897318
const FILTER_CARBON_DIOXIDE3 = 1876847024
const FILTER_NITROGEN1 = 632853248
const FILTER_NITROGEN2 = -632657357
const FILTER_NITROGEN3 = -1387439451
const FILTER_NITROUS1 = -1247674305
const FILTER_NITROUS2 = 1824284061
const FILTER_NITROUS3 = 465267979
const FILTER_OXYGEN1 = -721824748
const FILTER_OXYGEN2 = -1067319543
const FILTER_OXYGEN3 = -1217998945
const FILTER_POLLUTANT1 = 1915566057
const FILTER_POLLUTANT2 = 63677771
const FILTER_POLLUTANT3 = 1959564765
const FILTER_VOLATILE1 = 15011598
const FILTER_VOLATILE2 = 1037507240
const FILTER_VOLATILE3 = 1255156286

var shutdown_timer = 10
var current_state = STATE_IDLE
var next_state = STATE_FILTERING
var display_colour = COLOR_WHITE

start:

# Read the current states
var LeftFilterType = db.slot(FILTER_SLOT_LEFT).PrefabHash
var LeftFilter = db.slot(FILTER_SLOT_LEFT).Quantity
var RightFilter = db.slot(FILTER_SLOT_RIGHT).Quantity
var InputMoles = db.TotalMolesInput
var InputPressure = db.PressureInput
var OutputPressure = db.PressureOutput
var OutputPressure2 = db.PressureOutput2
max OutputPressure OutputPressure OutputPressure2

# Calculate the current filter canister state
var TotalFilter = LeftFilter + RightFilter
var AverageFilter = TotalFilter / 200

# Read the current input ratio depending on the cartridge type
var InputGasRatio = -1
InputGasRatio = db.RatioOxygenInput
switch LeftFilterType
    case FILTER_NITROGEN1
        InputGasRatio = db.RatioNitrogenInput
    end
    case FILTER_NITROGEN2
        InputGasRatio = db.RatioNitrogenInput
    end
    case FILTER_NITROGEN3
        InputGasRatio = db.RatioNitrogenInput
    end
    case FILTER_NITROUS1
        InputGasRatio = db.RatioNitrousOxideInput
    end
    case FILTER_NITROUS2
        InputGasRatio = db.RatioNitrousOxideInput
    end
    case FILTER_NITROUS3
        InputGasRatio = db.RatioNitrousOxideInput
    end
    case FILTER_CARBON_DIOXIDE1
        InputGasRatio = db.RatioCarbonDioxideInput
    end
    case FILTER_CARBON_DIOXIDE2
        InputGasRatio = db.RatioCarbonDioxideInput
    end
    case FILTER_CARBON_DIOXIDE3    
        InputGasRatio = db.RatioCarbonDioxideInput
    end
    case FILTER_POLLUTANT1
        InputGasRatio = db.RatioPollutantInput
    end
    case FILTER_POLLUTANT2
        InputGasRatio = db.RatioPollutantInput
    end
    case FILTER_POLLUTANT3    
        InputGasRatio = db.RatioPollutantInput
    end
    case FILTER_VOLATILE1
        InputGasRatio = db.RatioVolatilesInput
    end
    case FILTER_VOLATILE2
        InputGasRatio = db.RatioVolatilesInput
    end
    case FILTER_VOLATILE3    
        InputGasRatio = db.RatioVolatilesInput
    end
end

var input_gas_amount = InputGasRatio * InputMoles

# DETERMINE NEXT STATE
# blue:  idle->filtering
# grey:  filtering->waiting
# red:   filtering->idle(overp)
# green: waiting->filtering

# Idle -> Filtering
seq r15 current_state STATE_IDLE # if current_state == IDLE
beqz r15 state1_exit
sgt r15 input_gas_amount MIN_INPUT_MOLES # If input > min_input
beqz r15 state1_exit
move next_state STATE_FILTERING
display_colour = COLOR_BLUE
state1_exit:

# Filtering -> Waiting
seq r15 current_state STATE_FILTERING # if current_state == FILTERING
beqz r15 state2_exit
sle r15 input_gas_amount MIN_INPUT_MOLES # if input <= min_input
beqz r15 state2_exit
move next_state STATE_WAITING
display_colour = COLOR_GREY
state2_exit:

# Filtering -> Idle
seq r15 current_state STATE_FILTERING # if current_state == FILTERING
beqz r15 state3_exit
sge r15 OutputPressure MAX_OUTPUT_PRESSURE # if output >= max_output
beqz r15 state3_exit
move next_state STATE_IDLE
display_colour = COLOR_RED
state3_exit:

# Waiting -> Filtering
seq r15 current_state STATE_WAITING # if current_state == WAITING
beqz r15 state4_exit
sgt r15 input_gas_amount MIN_INPUT_MOLES # If input > min_input
beqz r15 state4_exit
move next_state STATE_FILTERING
display_colour = COLOR_GREEN
state4_exit:

# Waiting -> Idle
seq r15 current_state STATE_WAITING # if current_state == WAITING
beqz r15 state5_exit
sle r15 shutdown_timer 0 # if timer <= 0
beqz r15 state5_exit
move next_state STATE_IDLE
state5_exit:

# APPLY NEXT STATE
current_state = next_state

brne current_state STATE_IDLE 3
db.Mode = 0
# display_colour = COLOR_GREY
shutdown_timer = 10

brne current_state STATE_FILTERING 2
# display_colour = COLOR_GREEN
db.Mode = 1

brne current_state STATE_WAITING 2
# display_colour = COLOR_ORANGE
shutdown_timer = shutdown_timer - 1

# OUTPUT VALUES TO ATTACHED DISPLAYS
bdseal AverageFilterDisplay displayCartridgeAverage
bdseal InputRatioDisplay displayInputRatio

# LOOP BACK TO START
yield
j start

###############################################################################
# FUNCTIONS
###############################################################################

function displayCartridgeAverage
    AverageFilterDisplay.Mode = MODE_PERCENTAGE
    AverageFilterDisplay.Setting = AverageFilter
    AverageFilterDisplay.Color = display_colour
end

function displayInputRatio
    InputRatioDisplay.Mode = MODE_DEFAULT
    InputRatioDisplay.Setting = input_gas_amount
    InputRatioDisplay.Color = display_colour
end